// NonVisual Gaming Toolkit (NVGT)
// Copyright (C) 2022-2024 Sam Tupy
// license: zlib (see license.md in the root of the nvgt distrobution)

void main() {
	internet_request i("https://samtupy.com/wp-json/wp/v2/posts?_fields=id,title&tags=9");
	i.perform();
	while (!i.complete) wait(5);
	try {
		// If you can't completely trust the JSON source, it's best to either check it for errors or parse it in a try catch.
		var@ unknown_json = parse_json(i.response_body); // If we knew whether it would be an array or an object, we could use json_object@ or json_array@ instead of var@, instead we want to handle either case.
		json_array@ r = unknown_json;
		if (@r == null) { // failed to cast to array
			json_object@ o = unknown_json;
			throw ("from wordpress: " + string(o["message"]));
		}
		string o = r.size() + " posts: ";
		for (int i = 0; i < r.size(); i++) {
			json_object@ p = r[i];
			if (i < r.size() - 1) o += string(p("title.rendered")) + ", ";
			else o += "and " + string(p("title.rendered"));
		}
		alert("test", o);
	} catch {
		alert("oops?", get_exception_info());
	}
}
